sharedobject "libgtk-3.so"
sharedobject "libgio-2.0.so"

extern void g_print(string format);


extern void gtk_container_add(GtkContainer container,GtkWidget child);

extern void gtk_grid_attach (GtkGrid grid,
                 GtkWidget child,
                 int left,
                 int top,
                 int width,
                 int height);


extern void gtk_header_bar_pack_start(GtkHeaderBar header,GtkWidget child);

extern void gtk_window_set_titlebar(GtkWindow window,GtkHeaderBar titlebar);

extern int g_application_run(GApplication app,int argc,string & argv);

extern void gtk_application_add_window(GtkApplication app,GtkWindow win);

extern int gtk_dialog_run(GtkDialog dialog);

extern const GtkBox gtk_dialog_get_content_area(GtkDialog dialog);

extern void g_object_unref(object obj);

extern void gtk_widget_destroy(GtkWidget widget);

extern double atof(string text);

extern string g_strdup_printf(string format);

GtkApplication myapp = new GtkApplication {
  "application-id":"gnome.desktop.golem.testapp",
  "run": do () {
      g_application_run(myapp,0,"nocontest");
  },
  "add_window": do (GtkWindow window) {
      gtk_application_add_window(myapp,window);    
  }
};

myapp.on("activate", do() {
  
  GtkApplicationWindow main_window = new GtkApplicationWindow {    
    "default-width": 640,
    "default-height": 460,
    "visible": false
  };
  
  GtkNotebook notebook = new GtkNotebook {
    "show-border": false,
    "visible": true
  };
  
  GtkHeaderBar header_bar = new GtkHeaderBar {
    "show-close-button":true,
    "visible": true,
    "title": "Golem Test App"
  };
  
  GtkButton btn_connect = new GtkButton {
    "image": new GtkImage {
      "icon-name": "open-menu-symbolic",
      "visible":true
    },
    "visible": true
  };
  
  btn_connect.on("clicked",do () {
      g_print("clicked\n");
  });
 
  gtk_container_add(main_window,notebook);
  gtk_header_bar_pack_start(header_bar,btn_connect);
  gtk_window_set_titlebar(main_window,header_bar);
  myapp.add_window(main_window);
  
});

myapp.run();
